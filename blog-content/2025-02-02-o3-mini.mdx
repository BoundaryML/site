---
title: Structured outputs with o3-mini
description: How to do tool-calling or function-calling with o3-mini
slug: o3-mini-function-calling
date: Feb 2, 2025
tags: ['tutorials']
author:
  name: Vaibhav Gupta
  imageUrl: /profile-vbv.jpeg
  linkedin: https://www.linkedin.com/in/vaigup
---

OpenAI's o3-mini is a new reasoning model that can be used for tool-calling / function-calling.

Let's look at an example of how to use it with our prompting framework, [BAML](https://boundaryml.com), which lets you write prompts for structured extraction using a simple syntax.

### Solving a reasoning problem with a schema
Imagine we are trying to figure out the employee hierarchy chart for a company with this:

```
George is the CEO of the company. Kelly is the VP of Sales. Asif is the global head of product development. Mohammed manages the shopping cart experience. Tim manages sales in South. Stefan is responsible for sales in the f100 company. Carol is in charge of user experience"
```

Here is an interactive example that you can run to see how o3 can solve this reasoning problem!
<BamlBlock name="1" bamlCode={`
class Employee {
    name string
    title string
    subordinates Employee[]
}

function ExtractHierarchy(message: string) -> Employee {
    client "openai/o3-mini"
    prompt #"
      Extract the information.

      {{ ctx.output_format }}
      {{ _.role("user") }}
      {{ message }}
    "#
}

test TestName {
    functions [ExtractHierarchy]
    args {
      message #"
        George is the CEO of the company. Kelly is the VP of Sales. Asif is the global head of product development. Mohammed manages the shopping cart experience. Tim manages sales in South. Stefan is responsible for sales in the f100 company. Carol is in charge of user experience
      "#
    }
}
`}

 />

 You can run this in python like this:

```python
from baml_client import b
response = b.ExtractHierarchy(message="""
George is the CEO of the company.
Kelly is the VP of Sales. Asif is the global head of product development.
Mohammed manages the shopping cart experience.
Tim manages sales in South.
Stefan is responsible for sales in the f100 company.
Carol is in charge of user experience""")

print(response) # fully type-safe and validated!
```

### Tool calling with o3-mini and BAML
In BAML you can also use several tools. Here's another example

<BamlBlock name="2" bamlCode={`
class ItemSearch {
    query string @description("search query for the item")
    maxCost float @description("maximum cost filter")
    type string @description("item category filter")
}

class BookAppointment {
    clientName string
    serviceRequested string @description("type of service requested")
    datePreferred string @description("As an ISO8601 timestamp")
    timeDuration int @description("duration in minutes")
}

function ChooseOneTool(user_message: string) -> ItemSearch | BookAppointment {
    client "openai/o3-mini"
    prompt #"
     Choose the right schema that contains all the information in this message:
      ---
      {{ user_message }}
      ---

      {# special macro to print the output schema. #}
      {{ ctx.output_format }}

      JSON:
    "#
}

test TestOneFunc {
    functions [ChooseOneTool]
    args {
      user_message #"
        Find me running shoes under $100 in the sports category
      "#
    }
}

test TestOneFunc2 {
    functions [ChooseOneTool]
    args {
      user_message #"
        I need to schedule a haircut appointment for John Smith next Tuesday at 2pm for 30 minutes
      "#
    }
}
`}

 />
 You can call this in python like this (we also support other languages!):

```python
from baml_client import b
from baml_client.types import ProductSearch, ScheduleAppointment
response = b.ChooseOneTool(user_message="Find me running shoes under $100 in the sports category")
print(response)

if isinstance(response, ItemSearch):
    print(f"Item Search called:")
    print(f"Query: {response.query}")
    print(f"Max Price: ${response.maxPrice}")
    print(f"Category: {response.category}")
elif isinstance(response, BookAppointment):
    print(f"Book Appointment called:")
    print(f"Customer: {response.clientName}")
    print(f"Service: {response.serviceRequested}")
    print(f"Date: {response.datePreferred}")
    print(f"Duration: {response.timeDuration} minutes")
```

BAML is fully open-source and free to use, and it works with many other languages (Ruby, TS, Python, etc).

Check out the [docs](https://docs.boundaryml.com) for more!